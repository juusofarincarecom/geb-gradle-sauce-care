plugins {
    id "java"
    id "idea"
    id "groovy"
    id "com.github.erdi.webdriver-binaries" version "2.7"
    id "com.github.erdi.idea-base" version "2.2"
    id "org.gebish.saucelabs" version "7.0"
    id 'io.qameta.allure' version '2.11.2'
}

group = thegroup
version = theversion
description = "Geb SauceLabs for Care.com QA"

compileJava.sourceCompatibility = thesourceCompatibility
compileJava.targetCompatibility = thesourceCompatibility

ext {
    drivers = [
        //local drivers
        "firefox",
        "chrome",
        "chromeHeadless",
        //saucelabs drivers
        "edge_win",
        "firefox_mac",
        "chrome_mac",
        "safari_ios",
        //"chrome_android" Uncomment when appium+selenium is compatible again
        //https://github.com/appium/java-client/issues/1872#issuecomment-1484017595
    ]
    gebVersion = '7.0'
    seleniumVersion = '4.8.2'
    allureVersion = '2.21.0'
    junit5Version = '5.9.2'
}

repositories {
    mavenCentral()
    mavenLocal()
    google()
}
configurations {
    compile.exclude module: 'aspectjweaver'
}

dependencies {
    //implementation 'org.asynchttpclient:async-http-client-netty-utils:2.12.3'
    implementation group: 'org.aspectj', name: 'aspectjweaver', version: '1.9.19'
    
    // If using JUnit 5, need to depend on geb-junit5
    implementation "org.gebish:geb-junit5:$gebVersion"
    implementation "org.junit.jupiter:junit-jupiter-engine:${junit5Version}"
    implementation "org.junit.jupiter:junit-jupiter-params:${junit5Version}"
    implementation 'org.apache.groovy:groovy-all:4.0.11'
    
    //Allure
    implementation "io.qameta.allure:allure-junit5:$allureVersion"
    implementation "io.qameta.allure:allure-java-commons:$allureVersion"
    
    // Drivers
    implementation "org.seleniumhq.selenium:selenium-chrome-driver:$seleniumVersion"
    implementation "org.seleniumhq.selenium:selenium-firefox-driver:$seleniumVersion"
    implementation "org.seleniumhq.selenium:selenium-edge-driver:$seleniumVersion"
    implementation "org.seleniumhq.selenium:selenium-remote-driver:$seleniumVersion"
    implementation "org.seleniumhq.selenium:selenium-java:$seleniumVersion"
    implementation "org.seleniumhq.selenium:selenium-support:$seleniumVersion"
    
    //appium
    implementation group: 'io.appium', name: 'java-client', version: '8.3.0'
    
    // Sauce
    sauceConnect 'com.saucelabs:ci-sauce:1.168'
    
    //Logging
    implementation 'org.slf4j:slf4j-api:2.0.7'
    implementation 'org.slf4j:slf4j-simple:2.0.7'
    
}

webdriverBinaries {
    chromedriver {
        versionRegexp = '.*'
    }
    geckodriver {
        versionRegexp = '.*'
    }
}

drivers.each { driver ->
    task "${driver}Test"(type: Test) {
        group JavaBasePlugin.VERIFICATION_GROUP

        outputs.upToDateWhen { false }  // Always run tests

        useJUnitPlatform()
        systemProperty "geb.build.reportsDir", reporting.file("geb/$name")
        systemProperty "geb.env", driver
        systemProperty "org.aspectj.weaver.Dump.exception", "false"
    }
}

test {
    dependsOn drivers.collect { tasks["${it}Test"] }
    enabled = true
}


tasks.withType(Test) {
    maxHeapSize = "1g"
    jvmArgs '-XX:MaxMetaspaceSize=128m'
    testLogging {
        testLogging.showStackTraces = true
        testLogging.showStandardStreams = true
        testLogging.events "standardOut", "started", "passed", "skipped", "failed"
        testLogging.exceptionFormat "short"
    }
    maxParallelForks = System.getProperty("forks", "8") as Integer
    forkEvery = 1
}

tasks.withType(GroovyCompile) {
    groovyOptions.forkOptions.memoryMaximumSize = '256m'
}

task aggregateBuildInfo(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath
    systemProperties(System.properties)
    mainClass = "reporting.BuildInfoAggregator"
}

allure {
    version = allureVersion
}

[test].forEach {it.finalizedBy(aggregateBuildInfo) }